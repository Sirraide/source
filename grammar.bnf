// This grammar defines how Source is *parsed*; it does not convey
// any information about the semantics of the language. Operator
// precedence is also omitted.

<file> ::= <preamble> <stmts>

// ============================================================================
//  Preamble
// ============================================================================
//  Notes:
//
//   - IMPORT is a full keyword so the lexer knows the token after it is a
//     cxx-header-name if it starts w/ '<'.
//
//   - "program" and "module" are not because they are literally only keywords
//     if theyâ€™re the first token in the program.
//
<preamble>        ::= <header> { <import> }
<header>          ::= "program" <module-name> ";"
<import>          ::= IMPORT CXX-HEADER-NAME AS IDENTIFIER ";"
<module-name>     ::= IDENTIFIER

// ============================================================================
//  Statements
// ============================================================================
<stmts> ::= { <stmt> | <decl> }
<stmt>  ::= [ <expr> ] ";"
          | <expr-block>
          | <decl>
          | EVAL <stmt>

// ============================================================================
//  Expressions
// ============================================================================
<expr> ::= <expr-block>
         | <expr-binary>
         | <expr-call>
         | <expr-decl-ref>
         | <expr-eval>
         | <expr-lit>
         | <expr-member>
         | <expr-paren>
         | <expr-prefix>
         | <expr-return>
         | <expr-subscript>

<expr-binary>    ::= <expr> <binary> <expr>
<expr-block>     ::= "{" <stmts>  "}"
<expr-call>      ::= <expr> "(" [ <call-args> ] ")"
<expr-decl-ref>  ::= IDENTIFIER [ "::" <expr-decl-ref> ]
<expr-eval>      ::= EVAL <expr>
<expr-lit>       ::= STRING-LITERAL | INTEGER
<expr-member>    ::= <expr> "." IDENTIFIER
<expr-paren>     ::= "(" <expr> ")"
<expr-prefix>    ::= <prefix> <expr>
<expr-return>    ::= RETURN [ <expr> ]
<expr-subscript> ::= <expr> "[" <expr> "]" // Only one argument for now.

<call-args> ::= <expr> { "," <expr> } [ "," ]
<prefix> ::= "-" | "+" | "not" | "~"
<binary> ::= // TODO: list all binary operators here.


// ============================================================================
//  Declarations
// ============================================================================
<decl> ::= <decl-proc> | <decl-var>
<decl-var>   ::= <type> IDENTIFIER [ "=" <expr> ] ";"
<decl-proc>  ::= <signature> <proc-body>
<signature>  ::= PROC [ IDENTIFIER ] [ <proc-args> ] [ "->" <type> ]
<proc-args>  ::= "(" [ <param-decl> { "," <param-decl> } [ "," ] ] ")"
<proc-body>  ::= <expr-block> | "=" <expr> ";"
<param-decl> ::= <type> [ IDENTIFIER ] | <signature>

// ============================================================================
//  Types
// ============================================================================
<type> ::= <type-prim> | TEMPLATE-TYPE | <expr-decl-ref> | <signature>
<type-prim> ::= BOOL | INT | VOID | VAR
