// This grammar defines how Source is *parsed*; it does not convey
// any information about the semantics of the language. Operator
// precedence is also omitted.

<file> ::= <preamble> <stmts>

// ============================================================================
//  Preamble
// ============================================================================
//  Notes:
//
//   - IMPORT is a full keyword so the lexer knows the token after it is a
//     cxx-header-name if it starts w/ '<'.
//
//   - "program" and "module" are not because they are literally only keywords
//     if they’re the first token in the program.
//
<preamble>        ::= <header> { <import> }
<header>          ::= ( "program" | "module" ) <module-name> ";"
<import>          ::= IMPORT <import-name> AS ( IDENT | "* ) ";"
<module-name>     ::= IDENT
<import-name>     ::= IDENT | CXX-HEADER-NAME { "," CXX-HEADER-NAME } [ "," ]

// ============================================================================
//  Statements
// ============================================================================
<stmts> ::= { <stmt> }
<stmt>  ::= <expr-braces>
          | <expr-no-braces> ";"
          | <decl>
          | <stmt-defer>
          | <stmt-while>
          | <stmt-for>
          | <stmt-if>
          | EVAL <stmt>
          | LOOP <stmt>
          | ";"

<stmt-defer> ::= DEFER <stmt>
<stmt-while> ::= WHILE <expr> [ DO ] <stmt>
<stmt-if> ::= [ "#" ] IF <expr> <if-stmt-body> { [ "#" ] ELIF <expr> <if-stmt-body> } [ [ "#" ] ELSE <if-stmt-body> ]
<if-stmt-body> ::= [ THEN ] <stmt>

// Note: The support for trailing commas here was accidental, but I don’t really have a problem w/ it.
<stmt-for> ::= FOR [ <for-vars> IN ] <expr> { "," <expr> } [ "," ] DO <stmt>
<for-vars> ::= <idents> | ENUM IDENT [ "," <idents> ]
<idents>   ::= IDENT { "," IDENT } [ "," ]

// ============================================================================
//  Expressions
// ============================================================================
<expr>           ::= <expr-braces> | <expr-no-braces>
<expr-braces>    ::= <expr-block> | <expr-match>
<expr-no-braces> ::= <expr-assert>
                   | <expr-if>
                   | <expr-binary>
                   | <expr-break>
                   | <expr-call>
                   | <expr-continue>
                   | <expr-decl-ref>
                   | <expr-eval>
                   | <expr-lit>
                   | <expr-loop>
                   | <expr-member>
                   | <expr-paren>
                   | <expr-prefix>
                   | <expr-return>
                   | <expr-subscript>
                   | <expr-tuple>
                   | <expr-postfix>
                   | <type>

<expr-assert>    ::= [ "#" ] ASSERT <expr> [ "," <expr> ]
<expr-binary>    ::= <expr> <binary> <expr>
<expr-block>     ::= "{" <stmts>  "}"
<expr-break>     ::= BREAK
<expr-call>      ::= <expr> "(" [ <call-args> ] ")"
<expr-continue>  ::= CONTINUE
<expr-decl-ref>  ::= IDENT [ "::" <expr-decl-ref> ]
<expr-eval>      ::= EVAL <expr>
<expr-if>        ::= [ "#" ] IF <expr> <if-expr-body> { [ "#" ] ELIF <expr> <if-expr-body> } [ [ "#" ] ELSE <if-expr-body> ]
<expr-lit>       ::= STRING-LITERAL | INTEGER | TRUE | FALSE
<expr-loop>      ::= LOOP [ <expr> ]
<expr-match>     ::= MATCH [ <expr> ] [ "->" <type> ] "{" <match-case> [ ";" ] "}"
<expr-member>    ::= <expr> "." IDENT
<expr-paren>     ::= "(" <expr> ")"
<expr-prefix>    ::= <prefix> <expr>
<expr-return>    ::= RETURN [ <expr> ]
<expr-subscript> ::= <expr> "[" <expr> "]" // Only one argument for now.
<expr-tuple>     ::= "(" <expr> { "," <expr> } [ "," ] ")"
<expr-postfix>   ::= <expr> ( "++" | "--" )

<call-args> ::= <expr> { "," <expr> } [ "," ]
<prefix> ::= "-" | "+" | NOT | "~" | "&" | "^"
<if-expr-body> ::= [ THEN ] <expr>
<binary> ::= // TODO: list all binary operators here.

<match-case> ::= <pattern> ":" <stmt>
<pattern>    ::= <expr>

// ============================================================================
//  Declarations
// ============================================================================
<decl>        ::= <decl-proc> | <decl-var> | <decl-struct> | EXPORT <decl>
<decl-var>    ::= [ STATIC ] <type> IDENT [ "=" <expr> ] ";"
<decl-proc>   ::= <signature> <proc-body>
<decl-struct> ::= STRUCT IDENT "{" { <type> IDENT ";" } "}"
<signature>   ::= PROC [ IDENT ] [ <proc-args> ] <proc-attrs> [ "->" <type> ]
<proc-args>   ::= "(" [ <param-decl> { "," <param-decl> } [ "," ] ] ")"
<proc-attrs>  ::= { "native" | "extern" | "nomangle" | "variadic" }
<proc-body>   ::= <expr-block> | "=" <expr> ";" | ";"
<param-decl>  ::= [ <intent> ] <param-rest>
<param-rest>  ::= <type> [ "..." ] [ IDENT ] | <signature>
<intent>      ::= IN | OUT | INOUT | COPY

// ============================================================================
//  Types
// ============================================================================
<type> ::= <type-prim>
         | TEMPLATE-TYPE
         | <signature>
         | <type-qualified>
         | <type-range>
         | <expr-decl-ref> // Named type
         | <expr-tuple>    // Tuple type
         | <expr-binary>   // Array type (parsed as subscript expression)
         | <expr-call>     // Function that returns a type
         | "(" <expr> ")"  // Any expression in parens can be treated as a type
         | <expr>          // In type-only contexts

<type-qualified> ::= <type> { <qualifier> }
<type-range> ::= RANGE "<" <type> ">"
<qualifier> ::= "[" "]" | "^" | "?"
<type-prim> ::= BOOL | INT | TYPE | VOID | VAR | INTEGER_TYPE

// ============================================================================
//  Pragmas
// ============================================================================
// These are handled by the lexer and may thus appear anywhere.
<pragma> ::= <pragma-include>
<pragma-include> ::= PRAGMA "include" STRING-LITERAL
